apply plugin: 'java-library'
apply plugin:'application'
repositories {
	jcenter()
}

mainClassName = 'fb.Main'

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
	// Include dependencies in jar
	from {
		(configurations.runtime).collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
	// Set entry point
	manifest {
		attributes("Main-Class": mainClassName )
	}
}

run {
	// Get run arguments from runArgs property
	if (project.hasProperty('runArgs')) {
 		args findProperty('runArgs') 
 	}
 	// Set up standard in (for passwords during database import)
 	standardInput = System.in
}

tasks.withType(JavaExec) {
    jvmArgs = ['-Xss16m']
}

dependencies {
	
	// Stuff required for embedded jax-rs (jersey, grizzly)
	compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-simple-http', version: '2.26'
	compile group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: '2.26'
	
	// Database access
	compile group: 'org.hibernate', name: 'hibernate-core', version: '5.2.12.Final'
	compile group: 'org.postgresql', name: 'postgresql', version: '42.1.4'
	
	// HTMLEscaper
	compile group: 'com.google.guava', name: 'guava', version: '23.0'
	
	// Json
	compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
		
	// Markdown (commonmark)
	compile group: 'com.atlassian.commonmark', name: 'commonmark', version: '0.10.0'
	compile group: 'com.atlassian.commonmark', name: 'commonmark-ext-autolink', version: '0.10.0'
	compile group: 'com.atlassian.commonmark', name: 'commonmark-ext-gfm-strikethrough', version: '0.10.0'
	
	// Password hashing
	compile group: 'org.mindrot', name: 'jbcrypt', version: '0.4'
	
	// Email address validation
	compile group: 'commons-validator', name: 'commons-validator', version: '1.6'
	
	// Sending email
	compile group: 'com.sun.mail', name: 'javax.mail', version: '1.6.0'
	
	// RSS
	compile group: 'com.rometools', name: 'rome', version: '1.9.0'
	
}
